grammar org.xtext.example.mydsl.Sparql with org.eclipse.xtext.common.Terminals

generate sparql "http://www.xtext.org/example/mydsl/SPARQL"


import "http://www.eclipse.org/emf/2002/Ecore" as ecore


Query:
    prologue=Prologue selectQuery=SelectQuery;

Prologue:
    {Prologue} (prefixDecl+=PrefixDecl)*;

PrefixDecl:
    'PREFIX' name=PrefixName iri=IRIREF;

SelectQuery:
    'SELECT' variables+=Var whereClause=WhereClause;

WhereClause:
    'WHERE' '{' triplesBlock=TriplesBlock '}';

TriplesBlock:
    triplesSameSubject+=TriplesSameSubject ('.' triplesSameSubject+=TriplesSameSubject)* ('.');

TriplesSameSubject:
    subject=VarOrIri verb=Verb object=VarOrIri (';' properties+=Property)*;

Property:
    verb=Verb object=VarOrIri;

Verb:
    {Verb} isA='a' | {Verb} varOrIri=VarOrIri;

VarOrIri:
    variable=Var | iri=IRIREF | qname=Qname;

Var:
    '?' name=ID;


PrefixName returns ecore::EString:
	ID? ':'
;

Qname:
	{Qname} =>prefixName=PrefixName =>name=ID?
;

terminal DOUBLE:
	('-' | '+')? (('0'..'9')+ '.' ('0'..'9')* EXPONENT | '.' ('0'..'9')+ EXPONENT | ('0'..'9')+ EXPONENT)
;

terminal DECIMAL:
	('-'|'+')? (('0'..'9')+ '.' ('0'..'9')* | '.' ('0'..'9')+)
;

terminal EXPONENT:
	('e' | 'E') ('-'|'+')? ('0'..'9')+
;

terminal IRIREF returns ecore::EString:
	'<' -> '>'
;

@Override 
terminal ID : '^'?('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'-'|'0'..'9')*;
@Override
terminal SL_COMMENT : '#' !('\n'|'\r')* ('\r'? '\n')?;
@Override
terminal STRING:
			'"' ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|'"') )* '"' |
			"'" ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|"'") )* "'" |
			'"""' -> '"""'
		;