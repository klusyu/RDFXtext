grammar org.xtext.example.mydsl.RDFTurtle with org.eclipse.xtext.common.Terminals

generate rDFTurtle "http://www.xtext.org/example/mydsl/RDFTurtle"


import "http://www.eclipse.org/emf/2002/Ecore" as ecore

TurtleDoc:
	statements+=Statement*
;

Statement:
	(directive=Directive | triples=Triples) '.'
;

Directive:
	prefix=PrefixID | base=Base | sparqlPrefix=SparqlPrefix | sparqlBase=SparqlBase
;

PrefixID:
	'@prefix' prefixName=PrefixName iriref=IRIREF
;

Base:
	'@base' iriref=IRIREF
;

SparqlPrefix:
	'PREFIX' prefixName=PrefixName iriref=IRIREF
;

SparqlBase:
	'BASE' iriref=IRIREF
;

Triples:
	subject=Subject
	predicateObjectList=PredicateObjectList
;

PredicateObject:
	verb=Verb objectList=ObjectList
;

PredicateObjectList:
	predicateObjects+=PredicateObject (';' predicateObjects+=PredicateObject)* (';')?
;

ObjectList:
	objects+=Object (',' objects+=Object)*
;

Verb:
	predicate=Predicate | isA?='a'
;

Subject:
	resource=iResource | blank=Blank
;

Predicate returns iResource:
	iResource
;

Object:
	resource=iResource | blank=Blank | literal=Literal
;

LanguageString:
	qutoedString=QuotedString ('@' lang=ID)?
;

Literal:
	languageString=LanguageString | datatypeString=DatatypeString | dword=INT | real=DOUBLE | decimal=DECIMAL | bool=Boolean	
;

DatatypeString:
	qutoedString=QuotedString '^^' resource=iResource
;

Boolean returns ecore::EString:
	'true' | 'false'
;

Blank:
	nodeID=NodeID | isSquare?='[]' | '[' predicateObjectList=PredicateObjectList ']' | collection=Collection
;

ItemList:
	objects+=Object (objects+=Object)*
;

Collection:
	{Collection} '(' itemList=ItemList? ')'
;

iResource:
	iriRef=IRIREF | qname=Qname
;

NodeID:
	'_:' name=ID
;

Qname:
	{Qname} =>prefixName=PrefixName =>name=ID?
;

PrefixName returns ecore::EString:
	ID? ':'
;

QuotedString returns ecore::EString:
	STRING
;

terminal DOUBLE:
	('-' | '+')? (('0'..'9')+ '.' ('0'..'9')* EXPONENT | '.' ('0'..'9')+ EXPONENT | ('0'..'9')+ EXPONENT)
;

terminal DECIMAL:
	('-'|'+')? (('0'..'9')+ '.' ('0'..'9')* | '.' ('0'..'9')+)
;

terminal EXPONENT:
	('e' | 'E') ('-'|'+')? ('0'..'9')+
;

terminal IRIREF returns ecore::EString:
	'<' -> '>'
;

@Override 
terminal ID : '^'?('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'-'|'0'..'9')*;
@Override
terminal SL_COMMENT : '#' !('\n'|'\r')* ('\r'? '\n')?;
@Override
terminal STRING:
			'"' ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|'"') )* '"' |
			"'" ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|"'") )* "'" |
			'"""' -> '"""'
		;